ext.pomfileManualGenerator = {
    def root = asNode()

    def dependenciesNode = root.appendNode('dependencies')
    configurations.releaseRuntimeElements.allDependencies
            .findAll {
        it.name != null && it.name != 'unspecified' && it.version != 'unspecified' && !ext.pomFileBlacklist.contains(it.name)
    }.each {
        def dependencyNode = dependenciesNode.appendNode('dependency')
        dependencyNode.appendNode('groupId', it.group)
        dependencyNode.appendNode('artifactId', it.name)
        dependencyNode.appendNode('version', it.version)
        dependencyNode.appendNode('scope', 'compile')

        if (it.properties.excludeRules?.size() > 0) {
            def exclusionsNode = dependencyNode.appendNode('exclusions')
            it.properties.excludeRules.each { exclusion ->
                def exclusionNode = exclusionsNode.appendNode('exclusion')
                exclusion.group != null && exclusionNode.appendNode('groupId', exclusion.group)
                exclusionNode.appendNode('artifactId', exclusion.module != null ? exclusion.module : '*')
            }
        }
    }

    root.children().last()
}